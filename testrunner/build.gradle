apply plugin: 'java'
apply plugin: 'osgi'
apply plugin: 'alfresco-dynamic-extension'
apply plugin: "maven-publish"

project.ext.springVersion = '3.0.5.RELEASE'

group = 'com.github.dynamicextensionsalfresco'

configurations {
    embedded
    compile {
        extendsFrom embedded
    }
}

dependencies {
    compile "org.springframework:spring-core:${project.springVersion}"
    compile "org.springframework:spring-beans:${project.springVersion}"
    compile "org.springframework:spring-context:${project.springVersion}"
    compile "org.springframework:spring-test:${project.springVersion}"
    compile "org.slf4j:slf4j-api:1.7.2"
    compile "org.springframework.extensions.surf:spring-webscripts:1.0.0"
    compile "org.osgi:org.osgi.core:4.3.1"
    compile 'com.google.collections:google-collections:1.0'
    compile 'org.eclipse.gemini:org.eclipse.gemini.blueprint.core:1.0.2.RELEASE'

    embedded "junit:junit:4.10"
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
    repositories {
        maven {
            url project.hasProperty('detr_publish_url') ? project.detr_publish_url : ''
            credentials {
                username = project.hasProperty('detr_publish_username') ? project.detr_publish_username : ''
                password = project.hasProperty('detr_publish_password') ? project.detr_publish_password : ''
            }
        }
    }
}

jar {
    from(configurations.embedded) {
        into('lib')
    }
    manifest {
        instruction 'Bundle-Description', 'Integration testrunner extension'
        instruction 'Bundle-DocURL', 'https://github.com/laurentvdl/dynamic-extensions-testrunner'
        instruction 'Import-Package', '!org.junit.*,*'
        instruction 'Export-Package', 'junit.framework,org.junit,org.junit.runner,*'
        instruction 'Bundle-Classpath', ".,${configurations.embedded.files.collect({'lib/' + it.name}).join(',')}"
    }
}